<%doc>
# $Header: /tmp/netpass/NetPass/www/htdocs/Admin/qc.mhtml,v 1.18 2005/04/29 00:30:07 jeffmurphy Exp $

#   (c) 2004 University at Buffalo.
#   Available under the "Artistic License"
#   http://www.gnu.org/licenses/license-list.html#ArtisticLicense

</%doc>

<%args>
$ip     => '';
$mac    => '';
$uid    => '';
$search => '';
$submit => '';
$type   => 'AND';
$ids    => ();
$uqlinkup => '';
$status   => '';
</%args>

<script><!--
setWhereAmI('Quarantine Control');
--></script>

<%perl>

return unless $m->comp('/Admin/Login', %ARGS);

my @rwGroups = ('Admin', 'QuarAdmin');
my @roGroups = ('Reports');
my $readOnly = "disabled";
my @aclGroups = ();

my ($isRW, $rwGroups) = $m->comp('/Admin/MemberOf', 'acl' => [ @rwGroups ]);
my ($isRO, $roGroups) = $m->comp('/Admin/MemberOf', 'acl' => [ @roGroups ]);
my ($isRoot, $junk)   = $m->comp('/Admin/MemberOf', 'acl' => [ 'Admin' ], 'group' => 'default');

if ( $isRW ) {
	$readOnly = "";
} 
elsif ( $isRO ) {
	$readOnly = "disabled";
} 
else {
        print $q->p({-class=>'error'},
                "Sorry, you don't have permission to access this form.<P>");
	return;
}

my $statuses = ['QUAR','PQUAR','UNQUAR','PUNQUAR'];  # love the grammer!
my $msgs     = getAllMsgs($np->db->{'dbh'});

</%perl>

<h2>Quarantine Control</h2>

The "IP Address" field is the address that the client had <I>when they first registered</I>. It 
is <B>not necessarily</B> the address they have right now. So when quarantining a host, you
should really be searching by MAC Address. 

% use Sys::Hostname;
% if (hostname =~ /buffalo\.edu$/) {
To translate a currently assigned IP Address
into a MAC address, you can use some of the other tools available until we are able to
better integrate them into UB NetPass. <P>

These tools might require additional access. If you get an access denied, click on the 
"Apply for an account" link and apply for a  "Network Documentation" account. <P>
%}



<ul>
  <li> <a target="_blank" href="http://netstats.cit.buffalo.edu/Maps/Layer2Traceroute.cgi">Layer 2
       Traceroute</a>
  <li> <a target="_blank" href="http://netstats.cit.buffalo.edu/oss-bin/macip.cgi">IP to MAC Translator</a>
</ul>


<%perl>
print $q->start_form(-method => "POST");

print $q->table({-border=>0, -width=>600},
		$q->TR(
			$q->th({-colspan=>2}, "Search")
		).
		$q->TR(
			$q->td({-class=>"gray"}, "IP Address:") .
			$q->td({-class=>"gray"},
				$q->textfield(
						-name 	=> "ip",
						-value 	=> $ip,
						-filter => '/^\d+\.\d+\.\d+\.\d+$/',
						-error	=> 'Please format the IP Address in the format specified.'
				).
				$q->small(' e.g. '.$np->cfg->policy(-key => 'EXAMPLE_IPADDR', -network => $ENV{'REMOTE_ADDR'}))
			)
		).
		$q->TR(
			$q->td({-class=>"gray"}, "MAC Address:") .
			$q->td({-class=>"gray"},
				$q->textarea(
						-name 	=> "mac",
						-value 	=> $mac,
						-id     => 'mac',
						-cols   => 18, -rows => 1, 
						#-filter => '/^\w{2}:{0,1}\w{2}:{0,1}\w{2}:{0,1}\w{2}:{0,1}\w{2}:{0,1}\w{2}$/',
						#-error	=> 'Please format the MAC Address in the format specified.'
				).
				qq{<a href="javascript:clearField('mac');"><img 
                                     src="/resources/images/trashcan_full.png"
                                     alt="Clear Field" border='0' width=32 height=32
                                   ></a>  },

				$q->small('one per line')
			)
		).
		$q->TR(
			$q->td({-class=>"gray"}, $np->cfg->policy(-key => 'USERNAME_PROMPT', -network => $ENV{'REMOTE_ADDR'}) || "Username:") .
			$q->td({-class=>"gray"},
				$q->textfield(
						-name 	=> "uid",
						-value 	=> $uid, 
				)
			)
		).
		$q->TR(
			$q->td({-class=>"gray"}, "Status:") .
			$q->td({-class=>"gray"},
				$q->popup_menu(
						-name 	=> "status",
						-value 	=> [ '', @$statuses ],
						-default => $status
				)
			)
		).
		$q->TR(
			$q->td({-class=>"gray"}, "Unquar When?") .
			$q->td({-class=>"gray"},
                                        $q->popup_menu(
                                                        -name    => "uqlinkup",
                                                        -values  => [ '', 'no', 'yes' ],
                                                        -labels  => {
									"" => "",
                                                                        "no" => "Web Visit",
                                                                        "yes" => "Link Up"
                                                                     },
                                                        -default => $uqlinkup
				)
			)
		).
		$q->TR(
			$q->td({-class=>"gray"}, "Search Type:") .
			$q->td({-class=>"gray"},
				$q->popup_menu(
						-name 	 => "type",
						-values	 => [
								"AND",
								"OR"
							    ],
						-labels	 => {
								"AND" => "Results must match ALL of the above",
								"OR"  => "Results must match ANY of the above"
							    },
						-default => $type, 
				)
			)
		).
		$q->TR(
			$q->td({-class=>"gray"}, '&nbsp;') .
			$q->td({-class=>"gray"},
				$q->submit(
						-name 	=> "search",
						-value 	=> "Search"
				)
			)
		)
	);

goto end_of_page if(!$submit && !$search);

if($submit) {
	if ($readOnly eq "") {
        	my $sqr = saveQuarChanges($np->db->{'dbh'}, \%ARGS, $ids, $isRoot, $rwGroups, $roGroups);
		if ($sqr ne "") {
			print $q->p({-class=>"error"}, $sqr);
		}
	} else {
		# this really shouldnt happen. we dont give them a submit button
		$np->db->audit( -user     => $m->session->{'username'},
				-severity => "ALERT",
                                -msg      => [ "tried to change QuarControl but is ReadOnly" ]);

	}
}

my ($results, $count) = getQuars($np->db->{'dbh'}, $ip, $mac, $uid, $type, $status, $uqlinkup);

if($count<0) {
	print "Sorry, there was an error while processing your request! ($results)";
}

print $q->h4("Your search returned $count result(s)");

if($count==0) {
	print $q->p("You may want to re-define your search to be less strict.<br>I would suggest removing one or more fields and modifying the Search Type.");
	return;
}

my @rows = ();

push(@rows, $q->TR(
                   $q->td({-colspan=>8}, '&nbsp;').
                   $q->td({-colspan=>1, -align=>"right"},
                          $q->submit(-name  => "submit", -value => "Save Changes", $readOnly)
                   )
            )
);

push(@rows, $q->TR(
		   	$q->th("First Seen") .
		   	$q->th("Last Seen") .
		   	$q->th($np->cfg->policy(-key => 'USERNAME_PROMPT', -network => $ENV{'REMOTE_ADDR'}) || "Username") .
			$q->th("IP Address") .
			$q->th("Mac Address") .
			$q->th("Unquar<BR>When?") .
			$q->th("Status") .
			$q->th("History") .
			$q->th("Add Result")
	   )
);

while (my $row = $results->fetchrow_hashref() ) {
	push( @rows,	$q->TR( 
				$q->td({-align=>'center'}, $row->{lastSeen})    .
				$q->td({-align=>'center'}, $row->{registeredOn}) .
				$q->td({-align=>'center'}, $row->{username})     .
				$q->td({-align=>'center'}, $row->{ipAddress})   .
				$q->td({-align=>'center'}, "<code>", NetPass::padMac($row->{macAddress})), "</code>"   .
                                $q->td({-align=>'center'},
                                        $q->popup_menu(
                                                        -name    => "uqlinkup:" . $row->{macAddress},
                                                        -values  => [ 'no', 'yes' ],
                                                        -labels  => {
                                                                        "no" => "Web Visit",
                                                                        "yes" => "Link Up"
                                                                     },
                                                        -default => $row->{uqlinkup}, $readOnly
                                        ), "\n",),

                                $q->td({-align=>'center'},
                                        $q->popup_menu(
                                                        -name    => "status:" . $row->{macAddress},
                                                        -values  => $statuses,
                                                        -labels  => {
                                                                        "" => "Default Description"
                                                                     },
                                                        -default => $row->{status}, $readOnly
                                        ), "\n",
                                        $q->hidden(
                                                        -name    => "ids",
                                                        -value   => $row->{macAddress}
                                        ), "\n",
					$q->hidden( 	-name => 'ipAddr:' . $row->{macAddress},
							-value =>  $row->{ipAddress}
					), "\n",
					$q->hidden( 	-name => 'messageOrig:' . $row->{macAddress},
							-value =>  defne($row->{message}) ? $row->{message} : "--None--"
					), "\n",
					$q->hidden( 	-name => 'uqlinkupOrig:' . $row->{macAddress},
							-value =>  $row->{uqlinkup}
					), "\n",
					$q->hidden( 	-name => 'statusOrig:' . $row->{macAddress},
							-value =>  $row->{status}
					),
                                ) .
				$q->td({-align=>'center'},
					$q->a({-href => "clienthistory.mhtml?mac=".$row->{macAddress}},
						 "[ view ]")
				) . 
                                $q->td({-align=>'center'},
                                        $q->popup_menu(
                                                        -name    => "message:" . $row->{macAddress},
                                                        -values  => $msgs,
                                                        -default => defne($row->{message}) ?  $row->{message} : "--None--", $readOnly
                                        )
                                )
			));
}
$results->finish();

push(@rows, $q->TR(
                   $q->td({-colspan=>8}, '&nbsp;').
                   $q->td({-colspan=>1, -align=>"right"},
                          $q->submit(-name  => "submit", -value => "Save Changes", $readOnly)
                   )
            )
);

print $q->table({-width=>'80%'}, @rows);

end_of_page:;

print $q->p();
print $q->end_form();

#######################

sub defne {
	my $string = shift;
	return 0 if !defined($string);
	return 0 if ($string eq "");
	return 1;
}

sub getQuars {
	my $dbh	     = shift;
	my $ip       = shift;
	my $mac	     = shift;
	my $uid	     = shift;
	my $type     = shift;
	my $status   = shift;
	my $uqlinkup = shift;

	# double check the value, make sure noone can sneak an insertion attack in here ;)
	$type = ($type eq "OR")?"OR":"AND";
	
	my @clause = ();
	my @params = ();

	if($mac) {
		$mac =~ s/\n/ /g;
		$mac =~ s/\r/ /g;
		$mac =~ s/\s+/ /g;
		my @macList = split(/\s/, $mac);
		if ($#macList > 0) {
			my @_clause;
			for(my $ml = 0; $ml <= $#macList ; $ml++) {
				next unless $macList[$ml] ne "";
				$macList[$ml] =~ s/://g;
				$macList[$ml] = lc($macList[$ml]);
				push @_clause, "macAddress=?";
			}
			push(@clause, 
				" ( ". 
					join(" OR ", @_clause).
				" ) " );
			push(@params, @macList);
		} else {
			$mac =~ s/://g;
			$mac = lc($mac);
			push(@clause, "macAddress=?");
			push(@params, $mac);
		}
	}
	
	if($ip) {
		push(@clause, "ipAddress=?");
		push(@params, $ip);
	}
	
	if($uid) {
		push(@clause, "username=?");
		push(@params, $uid);
	}

	if($status) {
		push(@clause, "status=?");
		push(@params, $status);
	}

	if($uqlinkup) {
		push(@clause, "uqlinkup=?");
		push(@params, $uqlinkup);
	}

	my $query = "SELECT DATE_FORMAT(lastSeen, '%Y-%m-%d %H:%i:%s') as lastSeen, DATE_FORMAT(registeredOn, '%Y-%m-%d %H:%i:%s') as registeredOn, username, macAddress, ipAddress, OS, status, message, uqlinkup FROM register";
	
	if($#clause>-1) {
		$query .= " WHERE " . join(" $type ", @clause)
	}

	$query .= " ORDER BY registeredOn";

	#print "QUERY=$query<P>";
	#print "PARMS=",join(',', @params), "<P>";

	my $sth = $dbh->prepare($query);

	if(!defined($sth)) {
        	return ("prepare failed: ". $dbh->errstr, -1);
    	}
    
	if( !$sth->execute(@params) ) {
        	return ("execute failed: ". $dbh->errstr, -1);
    	}

    	my $count = $sth->rows;

	return ($sth, $count);

}

sub getAllMsgs {
        my $dbh = shift;

        my $query = "SELECT distinct(name) FROM pages order by name";
        my $sth = $dbh->prepare($query);
        $sth->execute();

        my @msgs = ('');

        while(my $m = $sth->fetchrow_arrayref) {
                push(@msgs, $m->[0]); 
        }

        $sth->finish;

	# sort msgs so that msg:\D are at the top and
	# msg:\d are at the bottom

	my @msgs_D;
	my @msgs_d;
	my @msgs_huh;

	foreach my $m (@msgs) {
		if ($m =~ /^msg:\D/) {
			push @msgs_D, $m;
		} 
		elsif ($m =~ /^msg:\d/) {
			push @msgs_d, $m;
		}
		else {
			push @msgs_huh, $m;
		}
	}

	@msgs = ('--None--', @msgs_D, @msgs_d, @msgs_huh);
        return \@msgs;

}

sub LA {
	my $l = shift;
	my $e = join(' ', @_);
	$m->comp('/Audit', msg => [ $e ]);
	_log($l, $e, "\n");
}

sub saveQuarChanges {
        my $dbh  = shift;
        my $args = shift;
        my $ids  = shift;
	my $isRoot   = shift;
	my $rwGroups = shift;
	my $roGroups = shift;

	my $D    = 0;

	if(!(ref($ids) =~ /ARRAY/) ) {
		$ids = [$ids];
	} 

	my $rv = "";
	my @rv;

	my $sql = "UPDATE register SET status=?, message=?, uqlinkup=? WHERE macAddress = ?";
        my $sth   = $dbh->prepare($sql);
	if (!defined($sth)) {
		return "Failed to prepare sql: ".$dbh->errstr;
	}

	#my $results   = { 'mac' => [], type => [], id => [] };
	my $resultsRV = '';

        foreach my $mac (@$ids) {
		# if nothing's changed, then dont bother with the db transaction

		my $m_unc = 1;
		my $s_unc = 1;
		my $u_unc = 1;

		print "debug: processing mac $mac<P><UL>\n" if $D;
		if ( ($args->{"message:$mac"} eq $args->{"messageOrig:$mac"}) ) {
			if ($D) {
				print "debug: message is unchanged.<P>\n";
				print qq{M=|$args->{"message:$mac"}|<P>\n};
				print qq{MO=|$args->{"messageOrig:$mac"}|<P>\n};
			}
			$m_unc = 1;
		} else {
			if ($D) {
				print "debug: message is changed.<P>\n";
				print qq{M=|$args->{"message:$mac"}|<P>\n};
				print qq{MO=|$args->{"messageOrig:$mac"}|<P>\n};
			}
			$m_unc = 0;
		}

		if ( ($args->{"status:$mac"} eq $args->{"statusOrig:$mac"}) ) {
			if ($D) {
				print "debug: status is unchanged.<P>\n";
				print qq{S=|$args->{"status:$mac"}|<P>\n};
				print qq{SO=|$args->{"statusOrig:$mac"}|<P>\n};
			}
			$s_unc = 1;
		} else {
			if ($D) {
				print "debug: status is changed.<P>\n";
				print qq{S=|$args->{"status:$mac"}|<P>\n};
				print qq{SO=|$args->{"statusOrig:$mac"}|<P>\n};
			}
			$s_unc = 0;
		}

		if ( ($args->{"uqlinkup:$mac"} eq $args->{"uqlinkupOrig:$mac"}) ) {
			if ($D) {
				print "debug: uqlinkup is unchanged.<P>\n";
				print qq{S=|$args->{"uqlinkup:$mac"}|<P>\n};
				print qq{SO=|$args->{"uqlinkupOrig:$mac"}|<P>\n};
			}
			$u_unc = 1;
		} else {
			if ($D) {
				print "debug: uqlinkup is changed.<P>\n";
				print qq{S=|$args->{"uqlinkup:$mac"}|<P>\n};
				print qq{SO=|$args->{"uqlinkupOrig:$mac"}|<P>\n};
			}
			$u_unc = 0;
		}

		print "</UL>\n" if $D;
		next if ( $m_unc && $s_unc && $u_unc );

		# we're being somewhat trusting here. to be thorough, refetch IP from
		# db based on mac

		my $nw       = $np->cfg->getMatchingNetwork(-ip => $args->{"ipAddr:$mac"});
		my $netgroup = $np->cfg->getNetgroup($nw);

		if ($nw eq "none") {
			print qq{<p class='error'>Update failed for $mac because it's network is unknown to NetPass.</P>};
			next;
		}

		if ( !$isRoot && !grep(/^$nw$/, @$rwGroups) && !grep(/^$netgroup$/, @$rwGroups) ) {
			print qq{<P class='error'>Update failed for $mac because you don't have the proper permissions for it's network ($nw)</P>};
			next;
		}

		if($D) {
			print "<PRE>[debugging output - ignore this]\n";
			print "mac ".$mac."\n";
			print "m |", $args->{"message:$mac"},      "|\n";
			print "mO |", $args->{"messageOrig:$mac"}, "|\n";
			print "s |", $args->{"status:$mac"},       "|\n";
			print "sO |", $args->{"statusOrig:$mac"},  "|\n";
			print "u |", $args->{"uqlinkup:$mac"},       "|\n";
			print "uO |", $args->{"uqlinkupOrig:$mac"},  "|\n";
			print "[end of debugging output]</PRE>\n";
		}

		my @params = ();
                push(@params, $args->{"status:$mac"});

		# if status is PQUAR then message is required.
		# if status is QUAR message is optional and can be NULL.
		# if status is P/UNQUAR the message is implicitly NULL

		if ($args->{"status:$mac"} eq "PQUAR") {
			if ($args->{"message:$mac"} eq "--None--") {
				return "Error: $mac PQUAR requires that you specify a message.";
			} else {
				push @params, $args->{"message:$mac"};
				$resultsRV = $np->db->addResult(-mac => $mac, -type => 'manual', 
								   -id => $args->{"message:$mac"});
			}
		}

		elsif ($args->{"status:$mac"} eq "QUAR") {
			if ($args->{"message:$mac"} eq "--None--") {
				push @params, undef;
			} else {
				push @params, $args->{"message:$mac"};
				$resultsRV = $np->db->addResult(-mac => $mac, -type => 'manual', 
								-force => 1,
								-id => $args->{"message:$mac"});
			}
		}

		elsif ($args->{"status:$mac"} =~ /^(UNQUAR|PUNQUAR)$/) {
			push @params, undef;
		} else {
			LA("ERROR", $m->session->{'username'}, 
				" invalid status specified for $mac ",
				$args->{"status:$mac"});
			print "Bad 'Status' setting. Skipping <code>$mac</code>.<BR>\n";
			next; # skip this one
		}

		if ($args->{"uqlinkup:$mac"} =~ /^(yes|no)$/i) {
			push @params, $args->{"uqlinkup:$mac"};
		} else {
			LA("ERROR", 
				$m->session->{'username'},
				" invalid uqlinkup specified for $mac " .
				$args->{"uqlinkup:$mac"});
			print "Bad 'Unquar When?' setting. Skipping <code>$mac</code>.<BR>\n";
			next; # skip this one
		}

                push(@params, $mac);


		LA("DEBUG", $m->session->{'username'}." is changing $mac - ".
			 join(',', @params));

		if ($args->{"message:$mac"} ne $args->{"messageOrig:$mac"}) {
			$np->db->audit(-mac => $mac, -ip => $args->{'ip'}, 
				-user => $m->session->{'username'},
			      	-msg => [ "QC: message changed to", $args->{"message:$mac"},
					  "from", $args->{"messageOrig:$mac"} ]);
		}

		if ($args->{"status:$mac"} ne $args->{"statusOrig:$mac"}) {
			$np->db->audit(-mac => $mac, -ip => $args->{'ip'}, 
				-user => $m->session->{'username'},
			      	-msg => [ "QC: status changed to", $args->{"status:$mac"},
					  "from",  $args->{"statusOrig:$mac"} ]);
		}


		
		#print "sql=", $sql, "<P>";
		#print "params=", join(", ", @params), "<P>";

		# if quarantine status has changed, then we need to instruct
		# the portmover to reset their port. if only the message is changed,
		# portmover doesnt need to know.

		# 1. is the IP active?
		# 2. does the mac match?
		# 3. what sw/po is it on?
		# 4. reset that port.


		$rv = $sth->execute(@params);

		if($rv) {
			$rv = "";
			my $ipAddr = $args->{"ipAddr:$mac"};
			print qq{<P class='error'>Database update succeeded for mac address "$mac" ($ipAddr)</P>};
			_log("DEBUG", "$mac $ipAddr record updated.\n");

			my ($sw, $po, $m2p, $p2m) = $np->findOurSwitchPort($mac, $ipAddr);

			if (!defined($sw) || !defined($po)) {
				$rv = "not found on network. Port reset failed.";

				_log("DEBUG", "$mac $ipAddr $rv\n");
				$np->db->audit(-mac => $mac, -ip => $args->{'ip'}, 
					-user => $m->session->{'username'},
			      		-msg => [ "QC: $rv " ]);

				$rv = qq{MAC address "$mac" $rv};
			} else {
				_log("DEBUG", "$mac $ipAddr found. quarantining $sw $po\n");
				if (! $np->db->requestMovePort(-switch => $sw, 
						-port => $po, -vlan => 'quarantine',
						-by => 'qc.mhtml')) {

					$rv = "$mac requestMovePort($sw, $po) failed\n";
					_log("ERROR", "$rv\n");

					$np->db->audit(-mac => $mac, -ip => $args->{'ip'}, 
						-user => $m->session->{'username'},
			      			-msg => [ "QC: $rv " ]);
				}

			}
		} else {
			$rv = "$mac failed to update database: ".$sth->errstr;
			_log("ERROR", "$rv\n");
			$np->db->audit(-mac => $mac, -ip => $args->{'ip'}, 
					-user => $m->session->{'username'},
		      			-msg => [ "QC: $rv " ]);
		}


		if ($resultsRV) {
			my $ipAddr = $args->{"ipAddr:$mac"};
			_log("ERROR", "$mac $ipAddr failed to addResult: $resultsRV\n");
			push @rv, "$mac failed to update database: $resultsRV";
		}

		# if any of the above failed, clean up and return a message
		# if nothing failed, we move on to the next mac address.

		if ($rv ne "") {
			$sth->finish;
			push @rv, $rv;
		}
        }

	$sth->finish;

	return join("<BR>", @rv);
}


</%perl>

<HR>

<h2>Definitions</h2>

<ul> 
<li> Quarantine Status </li> <BR>
<dl> <dt>QUAR</dt><dd>Quarantined</dd>
     <dt>PQUAR</dt><dd>Permanently Quarantined</dd>
     <dt>UNQUAR</dt><dd>Unquarantined</dd>
     <dt>PUNQUAR</dt><dd>Permanently Unquarantined</dd>
</dl>
<li> Unquar When? </li><BR>
<dl> <dt>Link Up</dt><dd>As soon as we see link come up on the client's port, validate them and
unquarantine the port assuming the client passes validation checks. In order for this 
setting to take effect, <code>UNQUAR_ON_LINKUP</code> must be set to <code>ITDEPENDS</code>
in <a href="gencfg.mhtml"><code>netpass.conf</code></a>. Validation checks are: 
all of the clients on the port must be tagged as "Link Up"
<I>and</I> they all must be registered and P/UNQUAR. If any client fails to match that, the port will not be
unquarantined.</dd>
     <dt>Web Visit</dt><dd>At least one client on this port must visit the NetPass web site
      before the port will be unquarantined.</dd>
</dl>
</ul>


<h2>Expected Behaviour</h2>

<ul>
<li> When setting a machine to UNQUAR or PUNQUAR:
<ul>
The port is set back to quarantined (yes, that's not a typo) 
within 10 seconds of clicking 'save changes'. 
The message field is implicitly set to 'None'. 
When they go to the website, it detects that they are unquarantined, changes their port 
and sends them to the success page. <P>

Presently, UNQUAR and PUNQUAR look/act the same. In the future, the IDS feature 
will use these. For example, you will be 
able to permanently unquarantine a host so that the IDS can not re-quarantine it.<P>
</Ul> 

<li> When setting a machine to QUAR or PQUAR:
<ul>
The port is changed to quarantined within 10 seconds of clicking 'save changes'. 
If you set a message, they are given that message when they access the web. 
Otherwise, if the message is "None", they are scanned.<P>

PQUAR requires that a message be set. QUAR does not require a message.
<P>

Presently, PQUAR and QUAR look/act the same. In the future, these will be used to 
implement the IDS "two strikes" feature by setting a message, but 
on the first strike you are only QUAR and the second strike you are PQUAR.
</ul>
</ul>




Making changes to a record results in an <a href="/Admin/audit.mhtml">audit log</a> entry.
